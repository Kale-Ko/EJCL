plugins {
    id "java"
    id "java-library"
    id "com.gradleup.shadow" version "8.3.3"
    id "maven-publish"
}

group = "io.github.kale_ko"
version = project_version

repositories {
    maven {
        name = "Kales-Maven"
        url = "https://maven.kaleko.dev/public-snapshot/"
    }
    mavenCentral()
}

dependencies {
    api "io.github.kale_ko:bjsl:${bjsl_version}"

    testImplementation "org.junit.jupiter:junit-jupiter:${junit_jupiter_version}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    compileOnly "org.jetbrains:annotations:${annotations_version}"

    runtimeOnly "com.mysql:mysql-connector-j:${mysql_connector_version}"
    runtimeOnly "org.mariadb.jdbc:mariadb-java-client:${mariadb_connector_version}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(java_version)
    }

    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

jar {
    manifest {
        attributes "Name": "io/github/kale_ko/ejcl/",
                "Specification-Title": "EJCL",
                "Specification-Version": project_version,
                "Specification-Vendor": "Kale-Ko",
                "Implementation-Title": "io.github.kale_ko.ejcl",
                "Implementation-Version": project_version,
                "Implementation-Vendor": "Kale-Ko",
                "Sealed": true
    }
}

shadowJar {
    archiveClassifier = "fat"
}

build {
    finalizedBy tasks.shadowJar
}

javadoc {
    title = project.name + " " + project.version

    options.encoding = "UTF-8"
    options.locale = "en"

    options.source = JavaLanguageVersion.of(java_version).asInt()

    options.showFromProtected()

    options.linkSource = true
    options.noDeprecatedList = true

    options.author = true
    options.version = true

    int javaVersion = JavaLanguageVersion.of(java_version).asInt()
    options.links = [javaVersion < 11 ? ("https://docs.oracle.com/javase/" + javaVersion + "/docs/api/") : ("https://docs.oracle.com/en/java/javase/" + javaVersion + "/docs/api/"),
                     "https://maven.kaleko.dev/javadoc/public-snapshot/io/github/kale_ko/bjsl/" + bjsl_version + "/raw/",
                     "https://javadoc.io/doc/org.jetbrains/annotations/" + annotations_version + "/"]

    options.tags = ["apiNote:TCMF:API note:", "implNote:TCMF:Implementation note:"]

    doFirst {
        mkdir "build/"
        mkdir "build/docs/"
        file("build/docs/overview.html").text = "<html><body><p class=\"overview\">" + project_description + "</p></body></html>"
    }

    options.overview "build/docs/overview.html"
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        mavenLocal()

        maven {
            name = "Kales-Maven-Snapshot"
            url = "https://maven.kaleko.dev/public-snapshot/"

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_TOKEN")
            }
        }

        if ("release".equalsIgnoreCase(System.getenv("MAVEN_TYPE"))) {
            maven {
                name = "Kales-Maven-Release"
                url = "https://maven.kaleko.dev/public-release/"

                credentials {
                    username = System.getenv("MAVEN_USER")
                    password = System.getenv("MAVEN_TOKEN")
                }
            }
            maven {
                name = "GitHub-Packages-Release"
                url = "https://maven.pkg.github.com/Kale-Ko/EJCL/"

                credentials {
                    username = System.getenv("GH_PACKAGES_USER")
                    password = System.getenv("GH_PACKAGES_TOKEN")
                }
            }
        }
    }

    publications {
        main(MavenPublication) {
            groupId = group
            artifactId = "ejcl"
            version = project_version

            artifact tasks.jar
            artifact tasks.shadowJar
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom.withXml {
                def node = asNode()

                node.appendNode("packaging", "jar")

                def licenseNode = node.appendNode("licenses").appendNode("license")
                licenseNode.appendNode("name", "MIT")
                licenseNode.appendNode("url", "https://opensource.org/licenses/MIT")

                def developerNode = node.appendNode("developers").appendNode("developer")
                developerNode.appendNode("id", "kale-ko")
                developerNode.appendNode("name", "Kale Ko")
                developerNode.appendNode("url", "https://www.kaleko.dev/")
                developerNode.appendNode("email", "contact@kaleko.dev")

                def scmNode = node.appendNode("scm")
                scmNode.appendNode("connection", "scm:git:git@github.com:Kale-Ko/EJCL.git")
                scmNode.appendNode("developerConnection", "scm:git:git@github.com:Kale-Ko/EJCL.git")
                scmNode.appendNode("tag", "main")
                scmNode.appendNode("url", "https://github.com/Kale-Ko/EJCL/")

                def dependenciesNode = node.appendNode("dependencies")

                def compileDependencies = []

                configurations.compileClasspath.resolvedConfiguration.getFirstLevelModuleDependencies().each {
                    def dependencyNode = dependenciesNode.appendNode("dependency")
                    dependencyNode.appendNode("groupId", it.moduleGroup)
                    dependencyNode.appendNode("artifactId", it.moduleName)
                    dependencyNode.appendNode("version", it.moduleVersion)
                    dependencyNode.appendNode("scope", "compile")

                    compileDependencies.push(it.moduleGroup + ":" + it.moduleName + ":" + it.moduleVersion)
                }

                configurations.runtimeClasspath.resolvedConfiguration.getFirstLevelModuleDependencies().each {
                    if (compileDependencies.contains(it.moduleGroup + ":" + it.moduleName + ":" + it.moduleVersion)) {
                        return
                    }

                    def dependencyNode = dependenciesNode.appendNode("dependency")
                    dependencyNode.appendNode("groupId", it.moduleGroup)
                    dependencyNode.appendNode("artifactId", it.moduleName)
                    dependencyNode.appendNode("version", it.moduleVersion)
                    dependencyNode.appendNode("scope", "runtime")
                }
            }
        }
    }
}